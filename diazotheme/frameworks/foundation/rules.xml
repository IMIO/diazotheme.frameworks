<?xml version="1.0" encoding="utf-8"?>
<rules xmlns="http://namespaces.plone.org/diazo"
       xmlns:css="http://namespaces.plone.org/diazo/css"
       xmlns:xhtml="http://www.w3.org/1999/xhtml"
       xmlns:xsl="http://www.w3.org/1999/XSL/Transform">

  <!-- The condition makes sure that theme is only applied when served in the 
       plone main_template. This is necessary for functional edit popups in 
       Tiny, for example. -->
  <theme href="index.html" css:if-content="#visual-portal-wrapper"/>

  <!-- Head rules
       ================================================================== -->
  <!-- Create conditional comment IE detection -->
  <before css:theme="html">

<!-- paulirish.com/2008/conditional-stylesheets-vs-css-hacks-answer-neither/ -->
<!--[if lt IE 7]> <html class="no-js lt-ie9 lt-ie8 lt-ie7" lang="en"> <![endif]-->
<!--[if IE 7]>    <html class="no-js lt-ie9 lt-ie8" lang="en"> <![endif]-->
<!--[if IE 8]>    <html class="no-js lt-ie9" lang="en"> <![endif]-->
<!--[if gt IE 8]><!--> </before>

<!-- Normally you would include the end of the conditional statement below here
     but since the html files included in the download already had the 
     conditionals around the html tag, you just need to insert the comments 
     outside of the html tag, because they get stripped by diazo. -->
<!--   <before css:theme-children="html"> -->
<!--<![endif]-->
<!--   </before> -->

  <!-- Attributes -->
  <copy attributes="*" css:theme="html" css:content="html"/>
  <copy attributes="*" css:theme="body" css:content="body"/>

  <!-- Pull in Plone Meta -->
  <before css:theme="title" css:content="meta" />

  <!-- Base tag -->
  <before css:theme-children="head" css:content="base"/>

  <!-- Title -->
  <replace css:theme="title" css:content="title" />

  <!-- Get Plone favicons and touch icons. -->
  <after css:theme="title" css:content="link[rel='shortcut icon']" />
  <after css:theme="title" css:content="link[rel='apple-touch-icon']" />
  
  <!-- Style -->
  <after css:theme-children="head">
<style>
    .ploneCalendar { width: 100%;  }
    .ploneCalendar th, .ploneCalendar td { padding: 4px; text-align: right; }
    .calendarHeader { text-align: center; }
    #calendar-previous { float: left; }
    #calendar-next { float: right; }
    #visual-portal-wrapper > footer { background-color: #eee; box-shadow: 0 8px 12px -12px #222 inset; text-align: center; }
    #visual-portal-wrapper > header { background-color: #222; box-shadow: 0 8px 12px -12px #222; text-align: center; }
    #portal-columns input[type='checkbox'], #portal-columns input[type='radio'] { float: left; position: relative; top: -4px; }
    header form { margin-top: 12px; margin-bottom: -12px; }
</style>
  </after>

  <!-- Scripts 
  <after css:theme-children="head" css:content="head script"/>
  -->

  <!-- Body rules
       ================================================================== -->
  <replace css:content=".documentDescription">
    <h5 class="subheader">
      <xsl:apply-templates select="./text()|./*"/>
    </h5>
  </replace>
  
  
  <!-- Columns -->
  <replace css:content=".width-full">
    <div class="twelve columns">
      <xsl:apply-templates select="./*"/>
    </div>
  </replace>
  <replace content="//*[contains(concat(' ', normalize-space(@class), ' '), ' width-1:2 ')]">
    <div class="six columns">
      <xsl:apply-templates select="./*"/>
    </div>
  </replace>
  <replace content="//*[contains(concat(' ', normalize-space(@class), ' '), ' width-1:3 ')]">
    <div class="four columns">
      <xsl:apply-templates select="./*"/>
    </div>
  </replace>
  <replace content="//*[contains(concat(' ', normalize-space(@class), ' '), ' width-2:3 ')]">
    <div class="eight columns">
      <xsl:apply-templates select="./*"/>
    </div>
  </replace>
  <replace content="//*[contains(concat(' ', normalize-space(@class), ' '), ' width-1:4 ')]">
    <div class="three columns">
      <xsl:apply-templates select="./*"/>
    </div>
  </replace>
  <replace content="//*[contains(concat(' ', normalize-space(@class), ' '), ' width-3:4 ')]">
    <div class="nine columns">
      <xsl:apply-templates select="./*"/>
    </div>
  </replace>

  <!-- Header -->
  <replace css:content="#portal-top">
    <header>
      <div class="row">
        <div class="twelve columns">
          <nav class="top-bar">
            <section>
              <!-- Left Nav Section -->
              <ul class="left">
                <xsl:for-each css:select="#portal-globalnav li">
                  <xsl:choose>
                    <xsl:when css:test=".selected">
                      <li class="active"><xsl:apply-templates css:select="a"/></li>
                    </xsl:when>
                    <xsl:otherwise>
                      <li><xsl:apply-templates css:select="a"/></li>
                    </xsl:otherwise>
                  </xsl:choose>
                </xsl:for-each>
              </ul>
              <!-- Right Nav Section -->
              <ul class="right">
                <li class="divider show-for-medium-and-up"></li>
                <xsl:choose>
                  <xsl:when css:test="dl#portal-personaltools">
                    <li><xsl:apply-templates css:select="#personaltools-logout"/> </li>
                  </xsl:when>
                  <xsl:otherwise>
                    <li><xsl:apply-templates css:select="#portal-personaltools a"/></li>
                  </xsl:otherwise>
                </xsl:choose>
              </ul>
            </section>
          </nav>
        </div>
      </div>
      <div class="row">
          <div class="three columns">
              <xsl:apply-templates css:select="#portal-logo"/>
          </div>
          <div class="nine columns">
              <xsl:apply-templates css:select="#portal-searchbox > form"/>
          </div>
      </div>          
      <hr />
    </header>
  </replace>
  <replace css:content="#portal-searchbox .LSBox">
    <div class="row collapse">
      <div class="ten mobile-three columns">
        <xsl:apply-templates css:select="#searchGadget"/>
        <label>
          <xsl:attribute name="for"><xsl:value-of select="./div[1]/input[1]/@id"/></xsl:attribute>
          <xsl:attribute name="class">left</xsl:attribute>
          <xsl:apply-templates select="./div[1]/input[1]"/>
          <xsl:value-of select="./div[1]/label[1]/text()"/>
        </label>
      </div>
      <div class="two mobile-one columns">
        <button class="button expand postfix">         
            <xsl:value-of select="./input[2]/@value"/>
        </button>
      </div>
    </div>
  </replace>
  <replace css:content="#portal-logo img">
      <img src="++theme++foundation-framework/logo.png" height="48" alt="Plone"/>
  </replace>
      

  <!-- Columns -->
  <replace content="//*[@id='portal-column-content' and contains(@class, 'width-3:4') and contains(@class, 'position-1:4')]">
      <div class="nine columns push-three">
          <xsl:apply-templates select="./*"/>
      </div>
  </replace>
  <replace content="//*[@id='portal-column-content' and contains(@class, 'width-1:2') and contains(@class, 'position-1:4')]">
      <div class="six columns push-three">
          <xsl:apply-templates select="./*"/>
      </div>
  </replace>
  <replace css:content="#portal-column-one:last-child">
      <div class="three columns pull-nine">
          <xsl:apply-templates select="./*"/>
      </div>
  </replace>
  <replace css:content="#portal-column-one:not(:last-child)">
      <div class="three columns pull-six mobile-two">
          <xsl:apply-templates select="./*"/>
      </div>
  </replace>
  <replace css:content="#portal-column-two:nth-child(3)">
      <div class="three columns mobile-two">
          <xsl:apply-templates select="./*"/>
      </div>
  </replace>

  <!-- Portlets -->
  <replace css:content=".portlet">
      <xsl:if css:test=".portletHeader">
          <h3 class="subheader"><xsl:apply-templates css:select=".portletHeader > *"/></h3>
      </xsl:if>
      <ul class="side-nav">
          <xsl:for-each css:select=".portletItem">
              <li>
                  <xsl:apply-templates select="./*"/>
              </li>
          </xsl:for-each>
      </ul>
      <xsl:if css:test=".portletFooter">
          <footer>
              <small>
                  <xsl:apply-templates css:select=".portletFooter"/>
              </small>
          </footer>
      </xsl:if>
      <hr/>
  </replace>
  <replace css:content=".portletNews, .portletEvents">
      <h3 class="subheader"><xsl:apply-templates css:select=".portletHeader > *"/></h3>
      <ul class="side-nav">
          <xsl:for-each css:select=".portletItem">
              <li>
                  <a>
                      <xsl:attribute name="href">
                          <xsl:value-of select="./a/@href"/>
                      </xsl:attribute>
                      <xsl:attribute name="title">
                          <xsl:value-of select="./a/@title"/>
                      </xsl:attribute>
                      <xsl:value-of select="./a/text()"/>
                      <small class="muted">
                          <xsl:copy-of select="./span"/>
                      </small>
                  </a>
              </li>
          </xsl:for-each>
      </ul>
      <xsl:if css:test=".portletFooter">
          <footer>
              <small>
                  <xsl:apply-templates css:select=".portletFooter"/>
              </small>
          </footer>
      </xsl:if>
      <hr/>
  </replace>
  <replace css:content=".portletNavigationTree">
      <ul class="nav-bar vertical navTreeLevel0">
          <xsl:if css:test=".portletHeader">
              <li class="list-header"><xsl:apply-templates css:select=".portletHeader > *"/></li>
              <li class="divider"></li>
          </xsl:if>
          <xsl:apply-templates select="./dd/ul/*"/>
          <xsl:if css:test=".portletFooter">
              <li class="divider"></li>
              <li><xsl:apply-templates css:select=".portletFooter > *"/></li>
          </xsl:if>
      </ul>
  </replace>
  <replace css:content=".portletStaticText">
      <xsl:if css:test=".portletHeader">
          <h3 class="subheader"><xsl:apply-templates css:select=".portletHeader > *"/></h3>
      </xsl:if>
      <xsl:apply-templates css:select=".portletItem"/>
      <xsl:apply-templates css:select="div.portletStaticText > *"/>
      <xsl:if css:test=".portletFooter">
          <footer><xsl:apply-templates css:select=".portletFooter"/></footer>
      </xsl:if>
  </replace>
  <replace css:content=".portletCalendar">
      <h3 class="calendarHeader subheader">
          <xsl:apply-templates css:select="#calendar-previous"/>
          <xsl:apply-templates select=".//caption/text()" />
          <xsl:apply-templates css:select="#calendar-next"/>
      </h3>
      <table class="ploneCalendar table table-hover table-condensed" summary="Calendar">
          <xsl:apply-templates select=".//thead" />
          <tbody class="table-bordered">
              <xsl:apply-templates select=".//tbody/tr" />
          </tbody>
      </table>
      <hr/>
  </replace>
  <strip css:content=".portletHeader"/>
  <strip css:content=".portletLogin br"/>
  <strip css:content="span[class*='portlet']"/>
  <strip css:content=".portletItem"/>
  <strip css:content=".portletFooter"/>
  
  <replace css:content="span.portletItemDetails">
      <small class="portletItemDetails muted">
          <xsl:apply-templates select="./text()" />
      </small>
  </replace>
  
  <!-- Footer -->
  <replace css:content="#portal-footer-wrapper">
    <footer>
      <hr />
      <div class="row">
          <div class="twelve columns">
              <xsl:apply-templates css:select="#portal-footer > *"/>
          </div>
          <div class="three columns mobile-two">
              <xsl:apply-templates css:select="#portal-colophon a"/>
          </div>
          <div class="nine columns mobile-two">
              <ul class="link-list right">
                  <xsl:apply-templates css:select="#portal-siteactions > li"/>
              </ul>
          </div>
      </div>
    </footer>
    <xsl:apply-templates select="./script"/>
  </replace>
  
  <!-- Breadcrumbs -->
  <drop css:content="#portal-breadcrumbs"/>
  <rules css:if-content="#breadcrumbs-1">
      <replace css:content="#viewlet-below-content" method="raw">
          <div id="#viewlet-below-content">
            <ul class="breadcrumbs">
                <xsl:for-each select="..//div[@id='portal-breadcrumbs']/span/*">
                  <xsl:if css:test=":not(.breadcrumbSeparator)">
                    <li>
                      <xsl:copy-of select="."/>
                    </li>
                  </xsl:if>
                </xsl:for-each>
            </ul>
          </div>
      </replace>     
  </rules>
  
  <!-- Forms -->
  <replace css:content=".formHelp">
      <p class="formHelp">
          <xsl:apply-templates select="./*|./text()"/>
      </p>
  </replace>
  <replace css:content="input[type='submit']">
    <button class="button"><xsl:value-of select="./@value"/></button>
  </replace>
  
  <replace css:content=".visualClear">
      <div class="clearfix">
          <xsl:apply-templates select="./text()" />
      </div>
  </replace>
  
  <!-- Copy over the content -->
  <replace css:theme="body" css:content="body" />

  <!-- Remove edit fixtures -->
  <drop css:content=".hiddenStructure"/>
  <drop css:content=".actionMenuContent"/>
  <drop css:content="#edit-bar"/>
  <drop css:content="#plone-lock-status"/>
  <strip css:content=".documentEditable"/>
  <strip css:content=".portletWrapper"/>
  <drop css:content=".documentAuthor"/>
  <drop css:content=".documentModified"/>
  <drop css:content=".contentHistory"/>
  <drop css:content=".portletRecent"/>
  <drop css:content=".managePortletsLink"/>
  <drop css:content=".managePortletsFallback"/>
  <drop css:content=".documentByLine"/>
</rules>