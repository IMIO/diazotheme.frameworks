<?xml version="1.0" encoding="UTF-8"?>
<rules xmlns="http://namespaces.plone.org/diazo"
         xmlns:css="http://namespaces.plone.org/diazo/css"
         xmlns:xsl="http://www.w3.org/1999/XSL/Transform"
         xmlns:xi="http://www.w3.org/2001/XInclude">

  <!-- Forms -->
  <replace css:content=".formHelp">
    <xsl:copy>
      <xsl:copy-of select="@*" />
      <xsl:attribute name="class"><xsl:value-of select="./@class" /> help-block</xsl:attribute>
      <xsl:apply-templates />
    </xsl:copy>
  </replace>

  <replace css:content="span.required">
    <span class="required text-info">(Required)</span>
  </replace>

  <replace content="//*[contains(@class, 'fieldRequired')]/@class">
    <xsl:attribute name="class"><xsl:value-of select="." /> text-info</xsl:attribute>
  </replace>

  <replace content="//div[@class='formQuestion label']/@class">
    <xsl:attribute name="class">formQuestion</xsl:attribute>
  </replace>

  <drop content="//label/span[@class='label']/@class" />

  <replace content="//div[@class='field']//div[@class='label']/@class">
    <xsl:attribute name="class"><xsl:value-of select="." /> label-info</xsl:attribute>
  </replace>

  <replace css:content="#content textarea, textarea#form.description">
    <xsl:copy>
      <xsl:copy-of select="@*" />
      <xsl:attribute name="class"><xsl:value-of select="./@class" /> input-block-level</xsl:attribute>
      <xsl:apply-templates />
    </xsl:copy>
  </replace>

  <replace css:content="form .widget">
    <xsl:copy>
      <xsl:copy-of select="@*" />
      <xsl:choose>
        <xsl:when css:test="[type='radio']">
            <xsl:attribute name="class"><xsl:value-of select="./@class" /> radio</xsl:attribute>
        </xsl:when>
        <xsl:when css:test="[type='checkbox']">
            <xsl:attribute name="class"><xsl:value-of select="./@class" /> checkbox</xsl:attribute>
        </xsl:when>
      </xsl:choose>
      <xsl:apply-templates />
    </xsl:copy>
  </replace>
  <drop css:content="form .widget .value br" />

  <replace css:content="form .field">
    <xsl:copy>
      <xsl:copy-of select="@*" />
      <xsl:choose>
         <xsl:when css:test=".error">
           <xsl:attribute name="class"><xsl:value-of select="./@class" /> control-group clearfix well well-small</xsl:attribute>
         </xsl:when>
         <xsl:otherwise>
           <xsl:attribute name="class"><xsl:value-of select="./@class" /> control-group clearfix</xsl:attribute>
         </xsl:otherwise>
      </xsl:choose>
      <div class="controls">
          <xsl:if test="not(*[@class='widget'])">
            <xsl:choose>
                <xsl:when css:test="[type='radio']">
                    <xsl:attribute name="class">controls radio</xsl:attribute>
                </xsl:when>
                <xsl:when css:test="[type='checkbox']">
                    <xsl:attribute name="class">controls checkbox</xsl:attribute>
                </xsl:when>
            </xsl:choose>
          </xsl:if>
        <xsl:apply-templates />
      </div>
    </xsl:copy>
        <!--
      <label>
        <xsl:copy-of select="./label/@*" />
        <xsl:attribute name="class"><xsl:value-of select="./label/@class" /> control-label</xsl:attribute>
        <xsl:apply-templates select="./label/text()"/>
        <xsl:apply-templates select="./span[@class='fieldRequired']"/>
      </label>
      <div class="controls">
        <xsl:apply-templates select="./*[not(self::label) and not(@class='fieldRequired')]"/>
      </div>
        -->

  </replace>

  <replace css:content="input">
      <xsl:choose>
          <xsl:when css:test="[type='submit'],[type='button']">
              <button>
                  <xsl:copy-of select="@*" />
                  <xsl:choose>
                      <xsl:when css:test=".delete, .destructive, [value='Delete']">
                          <xsl:attribute name="class"><xsl:value-of select="./@class" /> btn btn-danger</xsl:attribute>
                      </xsl:when>
                      <xsl:when css:test=".searchButton, .context">
                          <xsl:attribute name="class"><xsl:value-of select="./@class" /> btn btn-primary</xsl:attribute>
                      </xsl:when>
                      <xsl:when css:test=".standalone">
                          <xsl:attribute name="class"><xsl:value-of select="./@class" /> btn btn-info</xsl:attribute>
                      </xsl:when>
                      <xsl:otherwise>
                        <xsl:attribute name="class"><xsl:value-of select="./@class" /> btn</xsl:attribute>
                      </xsl:otherwise>
                  </xsl:choose>
                  <xsl:value-of select="./@value"/>
              </button>
          </xsl:when>
          <xsl:when css:test="[type='text'][name$='.title'],#title">
              <xsl:copy>
                  <xsl:copy-of select="@*" />
                  <xsl:attribute name="class"><xsl:value-of select="./@class" /> input-block-level</xsl:attribute>
                  <xsl:apply-templates />
              </xsl:copy>
          </xsl:when>
          <xsl:when css:test="[class='label checkboxType']">
              <xsl:copy>
                  <xsl:copy-of select="@*" />
                  <xsl:attribute name="class">checkboxType</xsl:attribute>
                  <xsl:apply-templates />
              </xsl:copy>
          </xsl:when>
          <xsl:otherwise>
              <xsl:copy-of select="."/>
          </xsl:otherwise>
      </xsl:choose>
  </replace>

  <rules css:if-content="body.template-search">
      <replace css:content=".actionMenu">
          <div class="btn-group search-menu">
              <button class="btn">
                  <xsl:value-of select="./dt/a"/>
              </button>
              <button class="btn dropdown-toggle" data-toggle="dropdown">
              <span class="caret"></span>
              </button>
              <ul class="dropdown-menu nav-list">
                  <xsl:for-each select="./dd//fieldset">
                      <li class="nav-header"><xsl:value-of select="./legend"/></li>
                      <li class="divider"></li>
                      <xsl:for-each select="./div//input[@type='checkbox']">
                          <li>
                              <label>
                                  <xsl:attribute name="for">
                                      <xsl:value-of select="following-sibling::label/@for"/>
                                  </xsl:attribute>
                                  <xsl:attribute name="class">checkbox</xsl:attribute> 
                                  <xsl:copy-of select="."/>
                                  <xsl:value-of select="following-sibling::label/text()"/>
                              </label>
                          </li>
                      </xsl:for-each>
                      <xsl:for-each select="./div//input[@type='radio']">
                          <li>
                              <label>
                                  <xsl:attribute name="for">
                                      <xsl:value-of select="following-sibling::label/@for"/>
                                  </xsl:attribute>
                                  <xsl:attribute name="class">radio</xsl:attribute> 
                                  <xsl:copy-of select="."/>
                                  <xsl:value-of select="following-sibling::label/text()"/>
                              </label>
                          </li>
                      </xsl:for-each>
                  </xsl:for-each>
                  <li class="divider"></li>
                  <li>
                      <button class="btn btn-primary">
                          <xsl:value-of select=".//input[@type='submit']/@value"/>
                      </button>
                  </li>
              </ul>
          </div>
        <script>
          (function($){
             $(document).ready(function() {
                $('.search-menu .dropdown-menu').click(function(event) { 
                    event.stopPropagation(); 
                });
             });
          })(jQuery);
        </script>
      </replace>
      <replace css:content="#searchResultsSort">
          <div class="clearfix">
              <xsl:copy-of select="."/>
          </div>
          <hr/>
      </replace>
  </rules>

</rules>
