<?xml version="1.0" encoding="UTF-8"?>
<rules xmlns="http://namespaces.plone.org/diazo"
         xmlns:css="http://namespaces.plone.org/diazo/css"
         xmlns:xsl="http://www.w3.org/1999/XSL/Transform"
         xmlns:xi="http://www.w3.org/2001/XInclude">

  <replace css:content=".managePortletsFallback">
    <div class="span12">
      <xsl:copy-of select="."/>
    </div>
  </replace>

  <replace css:content="table.listing">
    <table class="table table-striped">
      <xsl:copy-of select="./@*[local-name() != 'class']"/>
      <xsl:apply-templates select="./*"/>
    </table>
  </replace>

  <drop css:content="#edit-bar h5"/>

  <replace css:content="#content-views">
      <ul class="nav nav-tabs">
          <xsl:apply-templates select="./*"/>
      </ul>        
  </replace>

  <drop css:content=".contentActions .arrowDownAlternative"/>

  <replace css:content=".actionSeparator">
      <li class="divider"></li>
      <li>
          <xsl:apply-templates select="./*"/>
      </li>
  </replace>

  <!-- Rough translation of workflow state to text emphasis classes -->
  <replace content="//*[contains(@class, 'state-published') or contains(@class, 'state-external') or contains(@class, 'state-open') or contains(@class, 'state-expired')]/@class">
    <xsl:attribute name="class"><xsl:value-of select="." /> text-success</xsl:attribute>
  </replace>
  <replace content="//*[contains(@class, 'state-private') or contains(@class, 'state-closed')]/@class">
    <xsl:attribute name="class"><xsl:value-of select="." /> text-error</xsl:attribute>
  </replace>
  <replace content="//*[contains(@class, 'state-pending') or contains(@class, 'state-protected') or contains(@class, 'state-pending_external')]/@class">
    <xsl:attribute name="class"><xsl:value-of select="." /> text-warning</xsl:attribute>
  </replace>

  <replace css:content="#edit-bar > .contentActions">
      <div class="clearfix">
          <div class="btn-toolbar pull-right">
          <xsl:for-each select="./ul/li">
            <div class="btn-group">
                <button class="btn">
                    <xsl:apply-templates select=".//dt/a/span"/>
                </button>
                <button class="btn dropdown-toggle" data-toggle="dropdown">
                    <span class="caret"></span>
                </button>
                <ul class="dropdown-menu">
                    <xsl:apply-templates select=".//dd/ul/li"/>
                </ul>
            </div>
          </xsl:for-each>    
          </div>
      </div>
  </replace>

  <replace css:content=".criteria .querywidget">
    <div class="btn-group pull-left">
      <button class="btn">
          <xsl:apply-templates css:select=".multipleSelectionWidgetTitle"/>
      </button>
      <button class="btn dropdown-toggle" data-toggle="dropdown">
        <span class="caret"></span>
      </button>
      <ul class="dropdown-menu">
          <xsl:for-each select="./dd/*">
            <li>
              <xsl:apply-templates select="."/>
            </li>
          </xsl:for-each>
      </ul>
    </div>
  </replace>

  <replace css:content="dl.enableFormTabbing">
    <div>
      <xsl:copy-of select="@*" />
      <xsl:attribute name="class"><xsl:value-of select="./@class" /> accordion</xsl:attribute>
      <xsl:for-each select="./dt">
          <div class="accordion-group">
            <div class="accordion-heading">
              <a class="accordion-toggle" data-toggle="collapse">
                <xsl:attribute name="data-parent">#<xsl:value-of select="ancestor::dl[1]/@id"/></xsl:attribute>
                <xsl:attribute name="href">#<xsl:value-of select="./@id"/></xsl:attribute>
                <xsl:value-of select="./text()"/>
              </a>
            </div>
            <div>
              <xsl:copy-of select="@*" />
              <xsl:choose>
                  <xsl:when test="position() = 1">
                      <xsl:attribute name="class"><xsl:value-of select="./@class" /> accordion-body collapse in</xsl:attribute>
                  </xsl:when>
                  <xsl:otherwise>
                      <xsl:attribute name="class"><xsl:value-of select="./@class" /> accordion-body collapse out</xsl:attribute>
                  </xsl:otherwise>
              </xsl:choose>
              <div class="accordion-inner">
                <xsl:apply-templates select="./following-sibling::dd[1]/*"/>
              </div>
            </div>
          </div>
      </xsl:for-each>
    </div>
  </replace>

  <replace css:content="form.enableFormTabbing">
    <form>
      <xsl:copy-of select="@*" />
      <xsl:attribute name="class"><xsl:value-of select="./@class" /> accordion</xsl:attribute>
      <xsl:for-each select="./fieldset">
          <div class="accordion-group">
            <div class="accordion-heading">
              <a class="accordion-toggle" data-toggle="collapse">
                <xsl:attribute name="data-parent">#<xsl:value-of select="./ancestor::form[1]/@id"/></xsl:attribute>
                <xsl:attribute name="href">#<xsl:value-of select="./@id"/></xsl:attribute>
                <xsl:value-of select="./legend"/>
              </a>
            </div>
            <div>
              <xsl:copy-of select="@*" />
              <xsl:choose>
                  <xsl:when test="position() = 1">
                      <xsl:attribute name="class"><xsl:value-of select="./@class" /> accordion-body collapse in</xsl:attribute>
                  </xsl:when>
                  <xsl:otherwise>
                      <xsl:attribute name="class"><xsl:value-of select="./@class" /> accordion-body collapse out</xsl:attribute>
                  </xsl:otherwise>
              </xsl:choose>
              <div class="accordion-inner">
                <xsl:apply-templates select="./*[not(self::legend)]"/>
              </div>
            </div>
          </div>
      </xsl:for-each>
      <xsl:apply-templates select="./*[not(self::fieldset)]"/>
    </form>
  </replace>

  <replace css:content=".historyTools">
    <div>
      <xsl:copy-of select="@*" />
      <xsl:attribute name="class"><xsl:value-of select="./@class" /> pull-right</xsl:attribute>
      <ul class="unstyled">
        <xsl:apply-templates select="./ul/*"/>
      </ul>
    </div>
  </replace>

  <replace css:content=".historyRecord">
    <div>
      <xsl:copy-of select="@*" />
      <xsl:attribute name="class"><xsl:value-of select="./@class" /> well</xsl:attribute>
      <xsl:apply-templates select="./*"/>
    </div>
  </replace>

</rules>
